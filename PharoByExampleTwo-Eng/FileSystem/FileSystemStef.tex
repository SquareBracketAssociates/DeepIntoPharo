% $Author: ducasse $
% $Date: 2009-08-24 10:17:33 +0200 (Mon, 24 Aug 2009) $
% $Revision: 28563 $

% HISTORY:
% 2008-01-19 - Stef started
% 2008-12-26 - Jannick Menanteau added text

%=================================================================
\ifx\wholebook\relax\else
% --------------------------------------------
% Lulu:
	\documentclass[a4paper,10pt,twoside]{book}
	\usepackage[
		papersize={6.13in,9.21in},
		hmargin={.75in,.75in},
		vmargin={.75in,1in},
		ignoreheadfoot
	]{geometry}
	\input{../common.tex}
	\setboolean{lulu}{true}
% --------------------------------------------
% A4:
%	\documentclass[a4paper,11pt,twoside]{book}
%	\input{../common.tex}
%	\usepackage{a4wide}
% --------------------------------------------
    \graphicspath{{figures/} {../figures/}}
	\begin{document}
\fi
%=================================================================
%\renewmessage{\nnbb}[2]{} % Disable editorial comments
\sloppy
%=================================================================
\chapter{Files with FileSystem }\chalabel{filesystem}
\chapterauthor{\authorsteph{}}

A while ago Colin Putney announced the Filesystem framework, a nice and extensible replacement for the ugly FileDirectory classes in Pharo. While all core classes are well commented, there is a quick start missing that explains how end users are supposed to adopt the framework. 

\section{Getting started}
From Pharo 1.4 FileSystem is included by default in the system. If you want to load this library, use the following expression:

\begin{code}{}
Gofer new
  squeaksource: 'fs'; 
  package: 'ConfigurationOfFilesystem';
  load.
(ConfigurationOfFilesystem project version:  '2.0.1') load
\end{code}     

The framework supports different kinds of filesystems that can be used interchangeably and that can transparently work with each other. The most obvious one is the filesystem on your hard disk. We are going to work with that one for now:

\begin{code}{}
| working |
working := FSFilesystem onDisk workingDirectory.
\end{code} 

Type the above code into a workspace and evaluate it. It assigns a reference of the current working directory to the variable \ct{working}. References are the central objects of the framework and provide the primary mechanism of working with files and directories. They are instances of the class \ct{FSReference}.

Note that you do not use platform specific classes such as \ct{FSUnixStore} or \ct{FSWindowsStore}. All code below works on \ct{FSReference} instances.


\section{Navigating the Filesystem}

Now let's do some more interesting things. To list children of your working directory evaluate the following expression:

\begin{code}{}
working := FSFilesystem onDisk working.
working children.
\end{code} 

To recursively access all the children of the current directory you can use \ct{allChildren}

\begin{code}{}
working allChildren.
\end{code}


To get only jpeg files you can for example 
\begin{code}{}
working allChildren select: [ :each | each basename endsWith: 'jpeg' ]
\end{code} 
 
To get a reference to a specific file or directory within your working directory use the slash operator:

\begin{code}{}
cache := working / 'package-cache'.
\end{code} 

Navigating back to the parent is easy:

\begin{code}{}
cache parent.
\end{code}

You can check for various properties of the cache directory by evaluating the following expressions:

\begin{code}{}
 cache exists.             "--> true"
 cache isFile.             "--> false"
 cache isDirectory.        "--> true"
 cache basename.           "--> 'package-cache'"
\end{code} 
 
To get additional information about the filesystem entry evaluate:

\begin{code}{}
 cache entry creation.     "--> 2010-02-14T10:34:31+00:00"
 cache entry modification. "--> 2010-02-14T10:34:31+00:00"
 cache entry size.         "--> 0 (directories have size 0)"
\end{code} 

The framework also supports locations, late-bound references that point to a file or directory. When asking to perform a concrete operation, a location behaves the same way as a reference. Currently the following locations are supported:

\begin{code}{}
 FSLocator desktop.
 FSLocator home.
 FSLocator image.
 FSLocator vmBinary.
 FSLocator vmDirectory.
\end{code} 

If you save a location with your image and move the image to a different machine or operating system, a location will still resolve to the expected directory or file.

\subsection{Opening Read- and Write-Streams}

To open a file-stream on a file ask the reference for a read- or write-stream:

\begin{code}{}
 stream := (working / 'foo.txt') writeStream.
 stream nextPutAll: 'Hello World'.
 stream close.
 stream := (working / 'foo.txt') readStream.
 stream contents.
 stream close.
\end{code}

Please note that \ct{writeStream} overrides any existing file and \ct{readStream} throws an exception if the file does not exist. There are also short forms available:

\begin{code}{}
 working / 'foo.txt' writeStreamDo: [ :stream | stream nextPutAll: 'Hello World' ].
 working / 'foo.txt' readStreamDo: [ :stream | stream contents ].
\end{code}

Have a look at the streams protocol of FSReference for other convenience methods.

Renaming, Copying and Deleting Files and Directories

You can also copy and rename files by evaluating:

\begin{code}{}
 (working / 'foo.txt') copyTo: (working / 'bar.txt').
\end{code} 

To create a directory evaluate:
\begin{code}{}
 backup := working / 'cache-backup'.
 backup createDirectory.
\end{code} 

And then to copy the contents of the complete package-cache to that directory simply evaluate:

 cache copyAllTo: backup.
Note, that the target directory would be automatically created, if it was not there before.

To delete a single file evaluate:

 (working / 'bar.txt') delete.
To delete a complete directory tree use the following expression. Be careful with that one though.

 backup deleteAll.
 
That's the basic API of the Filesystem library. If there is interest we can have a look at other features and other filesystem types in a next iteration.



 working / 'foo.txt' readStreamDo: [ :stream | stream nextPutAll: 'Hello World' ].
 working / 'foo.txt' writeStreamDo: [ :stream | stream contents ].


\section{Design }
\sd{should add class comments and a uml diagram}

\subsection{Path}

Paths are the most fundamental element of the Filesystem API. They represent filesystem paths in a very abstract sense, and provide a high-level protocol for working with paths without having to manipulate Strings. Here are some examples using the methods that FSPath provides:

\begin{code}{}
    "absolute path"
    FSPath / 'plonk' / 'feep'       => /plonk/feep
    
    "relative path"
    FSPath * 'plonk' / 'feep'       => plonk/feep

    "relative path with extension"
    FSPath * 'griffle' , 'txt'      => griffle.txt
    
    "changing the extension"
    FSPath * 'griffle.txt' , 'jpeg'     => griffle.jpeg
    
    "parent directory"
    (FSPath / 'plonk' / 'griffle') parent   => /plonk
    
    "resolving a relative path"
    (FSPath / 'plonk' / 'griffle') resolve: (FSPath * '..' / 'feep')
                        => /plonk/feep
    
    "resolving an absolute path"
    (FSPath / 'plonk' / 'griffle') resolve: (FSPath / 'feep')
                        => /feep
                        
    "resolving a string"
    (FSPath * 'griffle') resolve: 'plonk'   => griffle/plonk
                        
    "comparing"
    (FSPath / 'plonk') contains: (FSPath / 'griffle' / 'nurp')
                        => false
\end{code}

\subsection{Filesystem}

A filesystem is an interface to some hierarchy of directories and files. "The filesystem," provided by the host operating system, is embodied by FSDiskFilesystem and it's platform-specific subclasses. But other kinds of Filesystems are also possible. FSMemoryFilesystem provides a RAM disk'a filesystem where all files are stored as ByteArrays in the image. FSZipFilesystem represents the contents of a zip file.

Each filesystem has its own working directory, which it uses to resolve any relative paths that are passed to it. Some examples:

\begin{code}{}
    fs := FSMemoryFilesystem new.
    fs workingDirectory: (FSPath / 'plonk').
    griffle := FSPath / 'plonk' / 'griffle'.
    nurp := FSPath * 'nurp'.
    
    fs resolve: nurp            => /plonk/nurp
    
    fs createDirectory: (FSPath / 'plonk')  => "/plonk created"
    (fs writeStreamOn: griffle) close.  => "/plonk/griffle created"
    fs isFile: griffle.         => true
    fs isDirectory: griffle         => false
    fs copy: griffle to: nurp       => "/plonk/griffle copied to /plonk/nurp"
    fs exists: nurp             => true
    fs delete: griffle          => "/plonk/griffle" deleted
    fs isFile: griffle          => false
    fs isDirectory: griffle         => false
\end{code}
	
\subsection{Reference}

Paths and filesystems are the lowest level of the Filesystem API. An FSReference combines a path and a filesystem into a single object which provides a simpler protocol for working with files. It implements the same operations as FSFilesystem , but without the need to track paths and filesystem separately:


\begin{code}{}
    fs := FSMemoryFilesystem new.
    griffle := fs referenceTo: (FSPath / 'plonk' / 'griffle').
    nurp := fs referenceTo: (FSPath * 'nurp').
    
    griffle isFile              
    griffle isDirectory 
    
    griffle parent ensureDirectory.     
    griffle writeStreamDo: [:s]         
    griffle copyTo: nurp            
    griffle delete              
\end{code}    

References also implement the path protocol, with methods like \ct{/} , \ct{parent} and \ct{resolve:}.

\subsection{Locator}

Locators could be considered late-bound references. They're left deliberately fuzzy, and only resolved to a concrete reference when some file operation needs to be performed. Instead of a filesystem and path, locators are made up of an origin and a path. An origin is an abstract filesystem location, such as the user's home directory, the image file, or the VM executable. When it receives a message like \ct{isFile}, a locator will first resolve its origin, then resolve its path against the origin.

Locators make it possible to specify things like "an item named 'package-cache' in the same directory as the image file" and have that specification remain valid even if the image is saved and moved to another directory, possibly on a different computer.

\begin{code}{}
    locator := FSLocator image / 'package-cache'.
    locator printString             => '{image}/package-cache'
    locator resolve                 => /Users/colin/Projects/Mason/package-cache
    locator isFile                  => false
    locator isDirectory             => true
\end{code}	

The following origins are currently supported:

\ct{image} - the image file
\ct{changes} - the changes file
\ct{vmBinary} - the executable for the running virtual machine
\ct{vmDirectory} - the directory containing the VM application (may not be the parent of \ct{vmBinary})
\ct{home} - the user's home directory
\ct{desktop} - the directory that hold the contents of the user's desktop
\ct{documents} - the directory where the user's documents are stored


Applications my also define their own origins, but the system will not be able to resolve them automatically. Instead, the user will be asked to manually choose a directory. This choice is then cached so that future resolution requests won't require user interaction.

\subsection{Enumeration}

References and Locators also provide simple methods for dealing with whole directory trees:

\begin{description}
\item[allChildren.]

This will answer an array of references to all the files and directories in the directory tree rooted at the receiver. If the receiver is a file, the array will contain a single reference, equal to the receiver.

\item[allEntries.]
This method is similar to \ct{allChildren}, but it answers an array of \ct{FSDirectoryEntries}, rather than references.

\item[copyAllTo: aReference.]

This will perform a deep copy of the receiver, to a location specified by the argument. If the receiver is a file, the file will be copied; if a directory, the directory and its contents will be copied recursively. The argument must be a reference that doesn't exist; it will be created by the copy.

\item[deleteAll.]

This will perform a recursive delete of the receiver. If the receiver is a file, this has the same effect as \ct{delete}.
\end{description}

\subsection{Visitors}

The above methods are sufficient for many common tasks, but application developers may find that they need to perform more sophisticated operations on directory trees.

The visitor protocol is very simple. A visitor needs to implement \ct{visitFile:} and \ct{visitDirectory:}. The actual traversal of the filesystem is handled by a guide. A guide works with a visitor, crawling the filesystem and notifying the visitor of the files and directories it discovers. There are three Guide classes, \ct{FSPreorderGuide}, \ct{FSPostorderGuide} and \ct{FSBreadthFirstGuide} , which traverse the filesystem in different orders. To arrange for a guide to traverse the filesystem with a particular visitor is simple. Here's an example:

\begin{code}{}
    FSBreadthFirstGuide show: aReference to: aVisitor
\end{code}	

The enumeration methods described above are implemented with visitors; see \ct{FSCopyVisitor}, \ct{FSDeleteVisitor}, and \ct{FSCollectVisitor} for examples.



%=========================================================
\ifx\wholebook\relax\else
   \bibliographystyle{jurabib}
   \nobibliography{scg}
   \end{document}
\fi

%=================================================================
\ifx\wholebook\relax\else\end{document}\fi
%=================================================================

%-----------------------------------------------------------------

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: t
%%% TeX-PDF-mode: t
%%% ispell-local-dictionary: "english"
%%% End: