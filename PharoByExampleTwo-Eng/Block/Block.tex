% $Author: oscar $
% $Date: 2009-08-16 16:37:09 +0200 (Sun, 16 Aug 2009) $
% $Revision: 28477 $

% HISTORY:
% 2008-01-19 - Stef started
% 2008-12-26 - Jannik Laval added text
% 2011-20-05 - Jean baptiste Arnaud add some text (Lexical closure)
% 2011-07-01 - Jean baptiste Arnaud add some test (Storing a block)
% 2011-08-09 - Stef doing another pass
% 2011-09-11 - Migrated to PharoBox: svn checkout https://XXX@scm.gforge.inria.fr/svn/pharobooks/PharoByExampleTwo-Eng
% todo for stef explaining blockClosure environment representation + explaining the trick with the bytecodes
% 2012-01-27 - Integrated Ben Coman feedback

%=================================================================
\ifx\wholebook\relax\else
% --------------------------------------------
% Lulu:
	\documentclass[a4paper,10pt,twoside]{book}
	\usepackage[
		papersize={6.13in,9.21in},
		hmargin={.75in,.75in},
		vmargin={.75in,1in},
		ignoreheadfoot
	]{geometry}
	\input{../common.tex}
	\setboolean{lulu}{true}
% --------------------------------------------
% A4:
%	\documentclass[a4paper,11pt,twoside]{book}
%	\input{../common.tex}
%	\usepackage{a4wide}
% --------------------------------------------
    \graphicspath{{figures/} {../figures/}}
	\begin{document}
\fi
%=================================================================
%\renewmessage{\nnbb}[2]{} % Disable editorial comments
\sloppy
%=================================================================
\chapter{Block and Dynamic Behavior of Smalltalk-Runtime}\chalabel{blocks}
\chapterauthor{Jean-Baptiste Arnaud}



Blocks (lexical closures) are a powerful and essential feature of Smalltalk. Without them it
would be difficult to have such a small and compact syntax. The use of blocks in Smalltalk
is the key to get conditionals and loops not hardcoded in the language syntax but just 
simple messages having blocks as arguments. This is why we can say that 
blocks work extremely well with the message passing syntax of Smalltalk.

In addition blocks are  effective to improve the readability, reusability and efficiency of code. 
However the dynamic runtime semantics of Smalltalk are often not well documented. Blocks in the presence of return statements behave like an escaping mechanism and while this can lead to ugly code when used to its extreme, it is important to understand it. 

In this chapter we will discuss some basic block behavior such as the notion of a static environment defined at block compile-time. Then we will present some deeper issues. But let us first recall some basics.

We already presented blocks in Pharo by Example. This presentation was simple and showing simply how to define blocks and to use them. Here we just will focus on deeper aspects and their run time behavior. 

\section{Basics}
What is a block? Historically, it's a Lambda expression, or an anonymous function. A block is a piece of code whose execution is frozen and kicked in using  specific messages.  Blocks are defined by square brackets. 

If you execute and print the result of the following block you will not get 3 but a block. 
\begin{code}[Block definition]{Block Definition}
[ 1 + 2 ]
    --> [ 1 + 2 ]
\end{code}

 A block is evaluated by sending the \mthind{BlockClosure}{value} message to it. More precisely blocks can be executed using \ct{value} (when no argument is mandatory), \ct{value:} (when one argument), \ct{value:value:}, \ct{value:value:value:}  and \ct{valueWithArguments: anArray}...). These messages are the basic and historical API for block execution. They were presented in Pharo by Example. 
 

\begin{code}[Block definition]{Block Execution}
[ 1 + 2 ] value  
    --> 3
    
[ :x | x + 2 ] value: 5
    --> 7
\end{code}

\subsection{Some handy extensions}
Pharo includes some handy messages such as \ct{cull:} and friends to support the execution of blocks even in presence of more values than necessary. This allows us to write blocks more concisely when we are not necessarily interested in all the available arguments.
\ct{cull:} fills the same need as  \ct{valueWith[Possible/Enough]Args:}, but does not require creating an Array with the arguments, and will raise an error if the receiver has more arguments than provided rather than pass nil in the extraneous ones. 
Hence, from where the block is provided, they look almost the same, but where the block is executed, the code is usually cleaner.

Here are some examples of \mthind{BlockClosure}{cull:} and  valueWithPossibleArgs: usages.
\begin{code}{Cull: examples}
[ 1 + 2 ] cull: 5
	--> 3
[ :x | 1 + 2 + x ] cull: 5 
	--> 8
[ 1 + 2 ] cull: 5 cull: 6
	--> 3
[ :x | 1 + 2 + x ] cull: 5 
	--> 8
[ :x | 1 + 2 + x ] cull: 5 cull: 3	
	--> 8 
[ :x :y | 1 + y + x ] cull: 5 cull: 2 
	--> 8
[ :x :y | 1 + y + x ] cull: 5 
	~->raises an error mentioning that the block requires two arguments.
[ :x :y | 1 + y + x ] valueWithPossibleArgs: #(5) 
	~->leads to an error because nil is passed as arguments.	
\end{code}

The message \mthind{BlockClosure}{once} is another extension that caches the results until the receiver is uncached. A typical usage is the following one:

\begin{method}{Example for resources caching using once}
myResourceMethod
	^ [expression] once
\end{method}

Blocks are instances of class \ct{BlockClosure}. The table below lists some of the messages available on this class. 
 
\begin{tabular}{p{2.5cm}|p{8cm}}
\textsf{silentlyValue}&Execute the receiver but avoiding progress bar notifications to show up.\\
\textsf{once}&Answer and remember the receiver value, answering exactly the same object in any further sends
	 of once or value. The expression will be evaluated once and its result returned for any subsequent evaluations.\\
\end{tabular}

Some messages are useful to profile execution (more information on Chapter~\ref{cha:Profiler}: 

\begin{tabular}{p{2.5cm}|p{8cm}}
\textsf{bench}&Return how many times the receiver can get executed in 5 seconds. \\
\textsf{durationToRun}&Answer the duration (instance of Duration) taken to execute the receiver block.\\
\textsf{timeToRun}&Answer the number of milliseconds taken to execute this block.\\
\end{tabular}

Some messages are related to error handling (as explained in the Exception Chapter~\ref{cha:exception}).

\begin{tabular}{p{2.5cm}|p{8cm}}
\textsf{ensure: aBlock}&Execute a termination block after evaluating the receiver, regardless of whether the receiver's evaluation completes.  \\
\textsf{ifCurtailed: aBlock}& Evaluate the receiver with an abnormal termination action. Evaluate aBlock only if execution is unwound during execution of the receiver. If execution of the receiver finishes normally do not evaluate aBlock. \\
\textsf{on: exception do: aBlock}&Evaluate the receiver. If an exception \textsf{exception} is raised, executes the block \textsf{aBlock}.\\
\textsf{on: exception fork: aBlock}&Execute the receiver. In case of exception, fork a new process, which will handle the error. The original process will continue running as if receiver evaluation finished and answered nil,\ie  an expression like: \textsf{[ self error: 'some error'] on: Error fork: [:ex |  123 ]} will always answer nil to the original process, not 123. The context stack, starting from context which sent this message to the receiver and up to the top of the stack will be transferred to the forked process, with handlerAction on top. When the forked process will resume, it will enter the block \textsf{aBlock}).\\
\end{tabular}


Some messages are related to process scheduling. We list the most important ones. Since this Chapter is not about concurrent programming in Pharo we will not go deep into them.

\begin{tabular}{p{2.5cm}|p{8cm}}
\textsf{fork}&Create and schedule a Process running the code in the receiver.\\
\textsf{forkAt: aPriority}& Create and schedule a Process running the code in the receiver at the given priority. Answer the newly created process. \\
\textsf{newProcess}&Answer a Process running the code in the receiver. The process is not scheduled.\\
\end{tabular}


\section{Variables and Blocks}
In Smalltalk, private variables (such as self, instance variables, temporaries and arguments) are 
lexically scoped. These variables are bound (get a value associated to them) in the context in which the block that contains them is defined, rather than the context in which the block is executed.  We call the context (a set of bindings) in which a block is defined, the \emph{block home context}.


Let's have fun and experiment a bit to understand. 
Define a class named \ct{BExp} (for BlockExperience) and the following methods:

\paragraph{Experience one.}\ 

\begin{code}{}
BExp>>testBlock: aBlock 
	| t | 
	t := nil. 
	aBlock value 
	
BExp>>testScope 
	| t | 
	t := 42. 
	self testBlock: [self traceCr: t printString] 
\end{code}

Execute \ct{BExp new testScope}. Executing the \ct{testScope} message will print 42 in the Transcript. The value of the temporary variable \ct{t} defined in method \ct{testScope} is the one used rather than the one of t defined inside \ct{testBlock:}.
The variable \ct{t} is not looked up in the context of the executing method \ct{testBlock:} but in the context of the method \ct{testScope} which defined the block.

\paragraph{Experience two.}\ 
\begin{code}{}
BExp>>testBlock: aBlock
	| t | 
	t := nil. 
	aBlock value 
	
BExp>>testScope2 
	| t | 
	t := 42. 
	self testBlock: [t := 33.
					self traceCr: t printString] 	
\end{code}

Executing \ct{BExp new testScope2} prints 33. This experience shows that a block is not only an anonymous method but one with an execution context or environment. In this environment temporary variables are bound with the values they hold when the block 
is defined. 

If we redefine \ct{testScope2} as follows: 

\begin{code}{}
testScope2 	|t|	t := 42.	self testBlock: [t := 33. self traceCr: t printString].	self testBlock: [t := 66. self traceCr: t printString].	self testBlock: [ self traceCr: t printString]
\end{code}

We will get 33, 66 and 66 printed.




\paragraph{For method arguments.}\
Naturally we can expect that method arguments as well as self and instance variables are bound 
in the context of defining method. Let's illustrate these points now. 

\begin{code}{}
BExp>>testScopeArgValue: aBlock
	| arg | 
	arg := 'zork'.
	aBlock value
	
BExp>>testScopeArg: arg
	"self new testScopeArg: 'foo'"
	
	self testScopeArgValue: [self traceCr: arg ; cr]
\end{code}

Now executing \ct{BExp new testScopeArg: 'foo'} prints 'foo' even if in the method \ct{testScopeArgValue:} the temporary arg is redefined.
 

\paragraph{self binding.}
For binding of self, we simply define a new class and a couple of methods. 
Add the instance variable x to the class \ct{BExp} and define the initialize method as follows:

\begin{code}{}
Object subclass: #BExp
	instanceVariableNames: 'x'
	classVariableNames: ''
	poolDictionaries: ''
	category: 'BlockExperiment'
\end{code}

\begin{code}{}
BExp>>initialize
	x := 123.
\end{code}	

Define another class named \ct{BExp2} (it can be a subclass of \ct{BExp} since inheritance is orthogonal to what we want to show).


\begin{code}{}
BExp2>>initialize
	super initialize.
	x := 69.

BExp2>>testScopeSelf: aBlock
	aBlock value
\end{code}

Then define the methods that will invoke  methods defined in \ct{BExp2}.
\begin{code}{}	
BExp>>testScopeSelf: aBlock
	"Here we ask another class to execute the block"
	BEXp2 new testScopeSelf: aBlock
	
BExp>>testScopeSelf
	"self new testScopeSelf"
	self testScopeSelf: [self traceCr: self printString ; traceCr: x]
\end{code}	

Now when we execute \ct{BExp new testScopeSelf}. You get \ct{a BExp123} printed, showing that a block captures self too, since an instance of BExp2 executed the block but the printed object was the original BExp instance. 

\paragraph{An example of sharing.}

Variables referred to by a block continue to be accessible and shared with other expressions. Let us  take some examples. Define the following method \ct{foo} which defines a temporary variable \ct{a}.

\begin{code}{}
BExp>>foo
	| a |
	[ a := 0 ] value.
	^ a
\end{code}

When we execute \ct{BExp new foo}, we get 0 and not nil. 
Here what you see is that the value is shared between the method body and the block. Inside the method body we can access the variable whose value was set by the block execution. 
Both the method  and block bodies access to the temporary variable \ct{a}.

Now imagine that we define the method \ct{foo} as follows:

\begin{code}{}
BExp>>foo
	| a |
	a := 0. 
	^ {[ a := 2] . [a]} 
\end{code}

The method \ct{foo} defines a temporary variable \ct{a}. It sets the value of \ct{a}
to zero and returns an array whose first element is a block setting the value to 2 and second element is a block just returning the value of the temporary variable. 

\begin{code}{}
| res | 
res := BExp new foo.
res second value.
     --> 0
res first value.
res second
     --> 2
\end{code}

You can also define the code as follows and open a transcript to see the results.

\begin{code}
| res |
res := BExp new foo.
res second value traceCr.
res first value.
res second value traceCr.
\end{code}

Let us step back and look at an important point. 
In the previous code snippet when the expressions \ct{res second value} and \ct{res first value} are executed, the method \ct{foo} has already finished its execution - as such it is not on the execution stack anymore.  Still the temporary variable \ct{a} can be accessed and set to new value. It means that the variables referred to by a block may live longer than the methods which created the block that refers to them. We say that the variables outlive their defining context. 

The block implementation needs to keep referenced variables in a structure that is not linked to the execution stack but lives in the heap. We will go in more details in a following section. 





\section{Returning from inside a block}
It is not a really good idea to have return statement in a block that you pass or or that store into instance variables and we will explain why in this section.

\subsection{Basics on Return}
A return statement allows one to return a different value than the receiver of the message.
Now a return expression behaves also like an escape mechanism since the execution flow will jump out to the current caller and not just one level up. For example, the following code will return 3 and 42 will never be reached. The expression \ct{[ ^3 ]} could be deeply nested, its execution jumps out all the levels.

\begin{code}{}
BExp>>foo
	
	#(1 2 3 4) do: [:each | self traceCr: each printString. 
						each = 3 
							ifTrue: [^ 3]].						
	^ 42
\end{code}

Now to see that a return is really escaping the current execution. 
We define 

\begin{code}{}
Foo>>start
	| res |
	self traceCr: 'start start'.
	res := self defineBlock.
	self traceCr: 'start end'.
	^ res
\end{code}

\begin{code}{}
Foo>>defineBlock
	| res |
	self traceCr: 'defineBlock start'.
	res := self arg: [ self traceCr: 'block start'. 
                            1 isZero ifFalse: !\textbf{[ \^{} 33 ]}!.
                            self traceCr: 'block end'. ].
	self traceCr: 'defineBlock end'.
	^ res
\end{code}

\begin{code}{}
Foo>>arg: aBlock
	| res |
	self traceCr: 'arg start'. 
	res := self arg2: aBlock.
	self traceCr: 'arg end'. 
	^ res 
\end{code}

\begin{code}{}
Foo>>arg2: aBlock
	| res |
	self traceCr: 'arg2 start'. 
	res := self arg2: aBlock value.
	self traceCr: 'arg2 end'. 
	^ res		
\end{code}




%\begin{code}{}
%testExplicitReturn
%	"self new testExplicitReturn"
%	
%	self traceCr: 'one'.
%	0 isZero ifTrue: [ self traceCr: 'two'. ^ self].
%	self traceCr: ' not printed'
%-> one two
%\end{code}



In Pharo \ct{^} should be the last statement of a block body. You should get a compile error if you type and compile the following expression. 

%\begin{code}{}
%[ self traceCr: 'two'.
%  ^ self.  
%  self traceCr: 'not printed' ]
%\end{code}



\subsection{Different blocks} \ja{need to be improved and code explained}

We can classify blocks based on their usage or not of return statement. 

\paragraph{Simple block.} \ct{[:x :y| x*x. x+y]} returns the value of the last statement to the method that sends it the message value. Here the first expression is useless.

\paragraph{Continuation blocks.} \ct{[:x :y| x*x. ^ x + y]} returns the value to the method that activated its homeContext. As a block is always evaluated in its homeContext, it is possible to attempt to return from a method which has already returned using other return. This runtime error condition is trapped by the VM.

\begin{code}{}
Object>>returnBlock
	"self new returnBlock value -> error"

	^[^self]

Object new returnBlock
-> Exception
\end{code}	
	
	
\begin{code}{}
|b| 
b:= [:x| Transcript show: x. x].
b value: a. b value: b.
b:= [:x| Transcript show: x. ^x].
b value: a. b value: b.
\end{code}
 
Continuation blocks cannot be executed several times!

\begin{code}{}
Test>>testScope
	   |t|
    	t := 15.
	   self testBlock: [Transcript show: "--",t printString, "--".
	   ^35 ].
    ^ 15

Test>>testBlock:aBlock
	   |t|
	   t := 50.
	   aBlock value.
	   self halt.
\end{code}

\begin{code}{}
Test new testBlock 	
print: *15* and not halt. 
return: 35
\end{code}


\begin{code}{}
|val|
val := [:exit |
		|goSoon|
		goSoon := Dialog confirm: 'Exit now?'.
		goSoon ifTrue: [exit value: 'Bye'].
		Transcript show: 'Not exiting'.
		'last value'] myValueWithExit.
Transcript show: val.
val
yes -> print Bye and return  Bye
no -> print Not Exiting 2 and return 2
\end{code}

\begin{code}{}
BlockClosure>>myValueWithExit
	      self value: [:arg| ^arg ].
      ^ '2'
BlockClosure>>myValueWithExit
 ^ self value: [:arg | ^ arg]        
\end{code}


\section{Lexical Closure}

\ja{english form must be verified}

Lexical closure is a concept introduced by SCHEME in 70s. Scheme uses lambda expression which is basically an anonymous function (such the block). But using anonymous function implies to connect it to the current execution context. \ja{please a verb}That why the lexical closure is important because it define when variables of block are bound to the execution context \ja{redo this sentence}. The variable is depending of the scope where it's \ja{no reduction in the text} define. Let's illustrate that :

\begin{code}{}
blockLocalTemp
	| collection |
		collection := OrderedCollection new.
		1 to: 3 do: [ :index || temp |
			temp := index. 
			collection add: [ temp ] ].
		^collection collect: [:each | each value].
\end{code}

Let's \ja{too much let's} comment the code, we create a loop the store the arg value, in a temporary variable created in the loop (then local) and change it in the loop. We store a block containing the simply temp read access in a collection. And after the loop, we evaluate each block and return the collection of value.
If we evaluate this method that will return \#(1 2 3). What's happen? At each loop we create a variable existing locally and bind it to a block. Then at the end evaluate block, we evaluate each block with this contextual \emph{temp}. \ja{should be redone}

\begin{figure}[htbp]
	\centering
        \includegraphics[width=0.7\linewidth]{blockClosureLocalTemp}
	\caption{blockLocalTemp Execution}
	\label{fig:blockLocalTempExecution}
\end{figure}


\newpage
Now see another case : 
\begin{code}{}
blockOutsideTemp
		| collection temp |
		collection := OrderedCollection new.
		1 to: 3 do: [ :index | 
			temp := index. 
			collection add: [ temp ] ].
		^collection collect: [:each | each value].
\end{code}
Same case except the \emph{temp}, variable will be  declare in the upper scope. Then what will happen? Here the temp at each loop is the \textbf{same} shared variable bind. So when we collect the evaluation of the block at the end we will collect \#(3 3 3).
\begin{figure}[htbp]
	\centering
		\includegraphics[width=0.7\linewidth]{blockClosureOutsideTemp}
	\caption{blockOutsideTemp Execution}
	\label{fig:blockClosureOutsideTemp}
\end{figure}




When we look at the following Scheme expression and evaluate it you get 4. Indeed a binding is created 
which associates the variable index to the value 0. Then y a lambda expression is defined and it returns
 the variable index (its value). Then within this context another expression is evaluated which starts
with a begin statement: first the value of the variable index is set to 4. Second the lambda expression is 
evaluated. It returns then the value of the 

\begin{code}{}
(let* ((index 0)
       (y (lambda () index)))
  (begin
    (set! index 4)
    (y)))
\end{code}

\begin{code}{}
(let ((index 0))
  (let ((y (lambda () index)))
    (begin
      (set! index 4)
      (y))))
\end{code}

\begin{code}{}
((lambda (index)
   ((lambda () (begin 
                (set! index 4)
                index))))
 0)
\end{code}


What you see is that the lambda expression is sharing the binding (index 0) with expression \ct{(begin...)}
therefore when this binding is modify from the body of the begin expression, the lambda expression sees its impact
and this is why it returns 4 and not 0 because. 


\section{To sort}




I have a method that takes OBCommands and returns Actions \ja{what is OBCommands and Actions? In the code I see GLMAction}:

\begin{code}{}
actionsFrom: aCollectionOfOBCommandClasses on: aTarget for: aRequestor
	| command |
	^ aCollectionOfOBCommandClasses collect: [ :each |
		command := each on: aTarget for: aRequestor.
		GLMAction new
			icon: command icon;
			title: command label;
			action: [:presentation | command execute ];
			yourself
		]
	\end{code}

These actions have a block that will be executed at a later time. The  
problem here was that the command in the action block was always  
pointing to the same command object, even at each point the command  
variable was populated correctly. \ja{I do not see the problem, should be more explicit}

However, when the command is defined inside the block, everything  
works as expected.

\begin{code}{}
actionsFrom: aCollectionOfOBCommandClasses on: aTarget for: aRequestor
	^ aCollectionOfOBCommandClasses collect: [ :each |
		| command |
		command := each on: aTarget for: aRequestor.
		GLMAction new
			icon: command icon;
			title: command label;
			action: [:presentation | command execute ];
			yourself
		]
	\end{code}




The semantics change in various ways. The trivial example that everyone 
knows is this \ja{everyone knows ??? are you sure ? You are saying to reader that if he does not know these 5 lines, he is an idiot !}:
\begin{code}{}
factorial := [ :n |
	n > 1
		ifTrue: [ n * (factorial value: n - 1) ]
		ifFalse: [ 1 ] ].
factorial value: 10.
\end{code}
Without closures you get an error, with closures you get the expected 
result. \ja{you affirms that but as a reader, I do not know why.}

Another significant change is the existence of local variables in blocks. 
Without closures blocks don't have local variables, with closures they do: \ja{do not understand this sentence}

\begin{code}{}
b := [ :p |
	| t |
	t ifNil: [ t := p ] ].
{ b value: 1. b value: 2 }
\end{code}

In a non-closure image you get \#(1 1) as result, because t is not a block 
local variable even if it looks like one. In a closure image you get \#(1 2)
because t is a block local variable. \ja{same as before, I see the result, but I do not understand why}

\ja{what is this source code ?}
\begin{code}{}
testValueWithExitBreak

	| val |	
	[ :break |
	    1 to: 10 do: [ :i |
			val := i.
			i = 4 ifTrue: [break value].
		] 
	] valueWithExit.
	self assert: val = 4.



testValueWithExitContinue

	| val last |	
	val := 0. 
	1 to: 10 do: [ :i |
		[ :continue |
			i = 4 ifTrue: [continue value].
			val := val + 1.
			last := i
		] valueWithExit.
	].
	self assert: val = 9.
	self assert: last = 10.	

BlockClosure>>valueWithExit 
	  self value: [ ^nil ]
\end{code}


\section{Blocks and Contexts}

\ja{have to be written}

VM represents the state of execution as Context objects
for method MethodContext
for block BlockContext

aContext contains a reference to the context from which it is invoked, the receiver arguments, temporaries in the Context

We call home context the context in which a block is defined


\paragraph{}
Arguments, temporaries, instance variables are lexically scoped in Smalltalk
These variables are bound in the context in which the block is defined and not in the context in which the block is evaluated




\section{Block Scope Optimization}

\ja{have to be written}

\section{Chapter conclusion}


We want to thank Ben Coman for his english corrections.
%=========================================================
\ifx\wholebook\relax\else
   \bibliographystyle{jurabib}
   \nobibliography{scg}
   \end{document}
\fi

%=================================================================
\ifx\wholebook\relax\else\end{document}\fi
%=================================================================

%-----------------------------------------------------------------

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: t
%%% TeX-PDF-mode: t
%%% ispell-local-dictionary: "english"
%%% End: