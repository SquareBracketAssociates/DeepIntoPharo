% $Author: ducasse $
% $Date: 2009-08-24 10:17:33 +0200 (Mon, 24 Aug 2009) $
% $Revision: 28563 $

% HISTORY:
% 2011-09-11 - Migrated to PharoBox: svn checkout https://XXX@scm.gforge.inria.fr/svn/pharobooks/PharoByExampleTwo-Eng
% 2011-09-17 - Stef integrated mariano feedback
% 2011-09-18 - Adding raw answers to our questions at the end of the file.


%=================================================================
\ifx\wholebook\relax\else
% --------------------------------------------
% Lulu:
	\documentclass[a4paper,10pt,twoside]{book}
	\usepackage[
		papersize={6.13in,9.21in},
		hmargin={.75in,.75in},
		vmargin={.75in,1in},
		ignoreheadfoot
	]{geometry}
	\input{../common.tex}
	\setboolean{lulu}{true}
% --------------------------------------------
% A4:
%	\documentclass[a4paper,11pt,twoside]{book}
%	\input{../common.tex}
%	\usepackage{a4wide}
% --------------------------------------------
    \graphicspath{{figures/} {../figures/}}
	\begin{document}
\fi
%=================================================================
%\renewmessage{\nnbb}[2]{} % Disable editorial comments
\sloppy
%=================================================================

\chapter{Fun with Floats}
\chapterauthor{\authornicolas{}}

Floats are inexact by nature and this can confuse programmers. This chapter introduces this problem and presents some practical solutions to it. The basic message is that Floats are what they are: inexact but fast numbers.

Note that most of the situations described in this chapters are consequences on how Floats are structured by the hardware and are not tied to Pharo. The very same problems in others programming languages.



\section{Never test equality on floats}
The first basic principle is to never compare float equality. 
Let's take a simple case: the addition of two floats may not be equal to the float representing their
sum. For example \ct{0.1 + 0.2} is not equal to \ct{0.3}.

\begin{code}{}
(0.1 + 0.2) = 0.3
	--> false
\end{code}

Hey, this is unexpected, you did not learn that in school, did you? This behavior is surprising indeed, but it's normal since floats are inexact numbers. What is important to understand is that the way floats are printed is also influencing our understanding. Some approaches print a simpler representation of reality than others. In early versions of Pharo printing \ct{0.1 + 0.2} were printing \ct{0.3}, now it prints \ct{0.30000000000000004}.
This change was guided by the idea that it is better not to lie to the user. Showing the inexactness of a float is better than hiding it because one day or another we can be deeply bitten by them. 

\begin{code}{}
(0.2 + 0.1) printString
	--> '0.30000000000000004' 

0.3 printString
	-->	'0.3'
\end{code}	

We can see that we are in presence of two different numbers by looking at the hexadecimal values. 

\begin{code}{}
(0.1 + 0.2) hex 
	--> '3FD3333333333334'
0.3 hex 
	--> '3FD3333333333333' 
\end{code}

	
The method \ct{storeString} also conveys that we are in presence of two different numbers.
		
\begin{code}{}
(0.1 + 0.2) storeString 
	--> 	'0.30000000000000004' 
0.3 storeString 
	-->	'0.3'
\end{code}	
		
\paragraph{About \ct{closeTo:}.} One way to know if two floats are probably close enough to look like the same number is to use the message \ct{closeTo:}

\begin{code}{}
(0.1 + 0.2) closeTo: 0.3
	--> true

0.3 closeTo: (0.1 + 0.2)
	--> true
\end{code}		

The method \ct{closeTo:} verify that the two compared numbers have less than 0.0001 of difference. Here is its source code.  

\begin{code}{}
closeTo: num
 	"are these two numbers close?"
	num isNumber ifFalse: [^[self = num] ifError: [false]].
	self = 0.0 ifTrue: [^num abs < 0.0001].
	num = 0 ifTrue: [^self abs < 0.0001].
	^self = num asFloat
		or: [(self - num) abs / (self abs max: num abs) < 0.0001]	
\end{code}

		
\paragraph{About Scaled Decimals.} There is a solution if you absolutely need exact floating point numbers: Scaled Decimals. They are exact numbers so they exhibit the behavior you expected.

\begin{code}{}
0.1s2 + 0.2s2 = 0.3s2
	--> true
\end{code}		

Now, if you execute the following line, you will see that the expressions are not equals.

\begin{code}{}	
(0.1 asScaledDecimal: 2) + (0.2 asScaledDecimal: 2) = (0.3 asScaledDecimal: 2) 
	--> false
\end{code}

What is different ? When you execute 0.1s2 for example, a fraction is created from scratch, hence 0.1s2 asFraction returns 1/10. The message \ct{asScaledDecimal:} has a different behavior.
\ct{(0.1 asScaledDecimal: 2)} represents exactly the same number as the float 0.1. \ct{(0.1 asScaledDecimal: 2) asFraction} returns the value \ct{0.1 asFraction}.
Since 0.1 + 0.2 = 0.3 returns false, you would of course expect the scaled decimal of this expression to return false.

\section{Dissecting a Float}
To understand what operation is involved in above addition, we must know how floats are internally represented in the computer: Pharo's Float format is a wide spread standard found on most computers - IEEE 754-1985 double precision on 64 bits
(See \url{http://en.wikipedia.org/wiki/IEEE_754-1985} for more details).
With this format, a Float is represented in base 2 by this formula: \[ sign \cdot mantissa \cdot 2^{exponent} \]
\begin{itemize}
\item The sign is represented with 1 bit.
\item The exponent is represented with 11 bits.
\item The mantissa is a fractional number in base two, with a leading 1 before decimal point, and with 52 binary digits after fraction point. In Pharo, the method to obtain the mantissa is \ct{Float>>significand}. We provide examples following in the chapter.
\end{itemize}

For example, a series of 52 bits:\\
\phantom{\ct{1.}}\ct{0110010000000000000000000000000000000000000000000000}\\
 means the mantissa is:\\
 \ct{1.0110010000000000000000000000000000000000000000000000}\\
 which also represents the following fractions:  \[ 1 + \frac{0}{2} +  \frac{1}{2^2} +  \frac{1}{2^3} +  \frac{0}{2^4} +  \frac{0}{2^5} +  \frac{1}{2^6} + \cdots  +  \frac{0}{2^{52}} \]
The mantissa value is thus between 1 (included) and 2 (excluded) for normal numbers.

\begin{code}{}
1 + ((1 to: 52) detectSum: [:i | (2 raisedTo: i) reciprocal]) asFloat 
	--> 1.9999999999999998
\end{code}




\paragraph{Building a Float.}
Let us construct such a mantissa:
\begin{code}{}
(#(0 2 3 6) detectSum: [:i | (2 raisedTo: i) reciprocal]) asFloat.
	--> 1.390625
\end{code}

Now let us multiply by $2^3$ to get a non null exponent:

\begin{code}{}
(#(0 2 3 6) detectSum: [:i | (2 raisedTo: i) reciprocal]) asFloat * (2 raisedTo: 3). 
	--> 11.125
\end{code}

Or using the method \ct{timesTwoPower:}

\begin{code}{}
(#(0 2 3 6) detectSum: [:i | (2 raisedTo: i) reciprocal]) asFloat timesTwoPower: 3.
	--> 11.125
\end{code}

In Pharo, you can retrieve these informations:
 \begin{code}{}
11.125 sign.
	--> 1
	
11.125 significand.
	--> 1.390625
	
11.125 exponent.
	--> 3
\end{code}

In Pharo, there is no message to directly handle the normalized mantissa. Instead it is possible to handle the mantissa as an Integer after a 52 bits shift to the left. There is one good reason for this: operating on Integer is easier because arithmetic is exact. The result includes the leading 1 and should thus be 53 bits long for a normal number (that's the float precision):


\begin{code}{}
11.125 significandAsInteger 
	--> 6262818231812096 
 
11.125 significandAsInteger printStringBase: 2.
	--> '10110010000000000000000000000000000000000000000000000'
	
'10110010000000000000000000000000000000000000000000000' size
	--> 53
	
11.125 significandAsInteger highBit.
	--> 53
	
Float precision.
	--> 53
\end{code}

You can also retrieve the \emph{exact} fraction corresponding to the internal representation of the Float:
 \begin{code}{}
 11.125 asTrueFraction.
	-->  (89/8)

(#(0 2 3 6) detectSum: [:i | (2 raisedTo: i) reciprocal]) * (2 raisedTo: 3).
	-->  (89/8)
\end{code}

Until there we've retrieved the exact input we've injected into the Float. Are Float operations exact after all? Hem, no, we only played with fractions having a power of 2 as denominator and a few bits in numerator. If one of these conditions is not met, we won't find any exact Float representation of our numbers. For example, it is not possible to represent 1/5 with a finite number of binary digits. Consequently, a decimal fraction like 0.1 cannot be represented exactly with above representation.
 \begin{code}{}
(1/5) asFloat = (1/5).
	--> false
	
(1/5) = 0.2
	--> false
\end{code}

Let us see in detail how we could get the fractional bits of \ct{1/5} \ie \mbox{\ct{2r1/2r101}}. For that, we must lay out the division:

\[
\begin{tabular}{r|l}
\multicolumn{1}{r|}{1\phantom{00000000}} & 101 \\  \cline{2-2}
10\phantom{0000000} & 0.00110011 \\ 
100\phantom{000000}  & \\
1000\phantom{00000}  & \\
-101\phantom{00000}  & \\
   11\phantom{00000}  & \\
   110\phantom{0000} & \\
  -101\phantom{0000} & \\
        1\phantom{0000} & \\
        10\phantom{000} & \\
        100\phantom{00} & \\
        1000\phantom{0} & \\
         -101\phantom{0} & \\
             11\phantom{0} & \\
             110 & \\
            -101 & \\
                  1 & \\
\end{tabular} 
 \]

What we see is that we get a cycle: every 4 Euclidean divisions, we get a quotient \ct{2r0011} and a remainder \ct{1}. That means that we need an infinite series of this bit pattern \ct{0011} to represent 1/5 in base 2. Let us see how Pharo dealt to convert (1/5) to a Float:

 \begin{code}{}
(1/5) asFloat significandAsInteger printStringBase: 2.
	--> '11001100110011001100110011001100110011001100110011010'
	
(1/5) asFloat exponent.
	--> -3
\end{code}

That's the bit pattern we expected, except the last bits \ct{001} have been rounded to upper \ct{010}. This is the default rounding mode of Float, round to nearest even. We now understand why \ct{0.2} is represented inexactly in machine. It's the same mantissa for \ct{0.1}, and its exponent is \ct{-4}.

\begin{code}{} 
0.2 significand
 	--> 1.6
	
0.1 significand 
	--> 1.6	
	
0.2 exponent
	--> -3
	
0.1 exponent
	--> -4
\end{code}


So, when we entered \ct{0.1} + \ct{0.2}, we didn't get exactly (1/10) + (1/5).
Instead of that we got:

 \begin{code}{}
0.1 asTrueFraction + 0.2 asTrueFraction.
	-->  (10808639105689191/36028797018963968)
\end{code}

But that's not all the story... Let us inspect the bit pattern of above fraction, and check the span of this bit pattern, that is the position of highest bit set to 1 (leftmost) and position of lowest bit set to 1 (rightmost):
 \begin{code}{}
10808639105689191 printStringBase: 2.
	-->  '100110011001100110011001100110011001100110011001100111'
	
10808639105689191 highBit.
	-->  54
	
10808639105689191 lowBit.
	-->  1

36028797018963968 printStringBase: 2.
	-->  '10000000000000000000000000000000000000000000000000000000'
\end{code}

The denominator is a power of 2 as we expect, but we need 54 bits of precision to store the numerator... Float only provides 53. There will be another rounding error to fit into Float representation:

\begin{code}{}
(0.1 asTrueFraction + 0.2 asTrueFraction) asFloat = (0.1 asTrueFraction + 0.2 asTrueFraction).
	--> false
	
(0.1 asTrueFraction + 0.2 asTrueFraction) asFloat significandAsInteger.
	--> '10011001100110011001100110011001100110011001100110100'
\end{code}

To summarize what happened, including conversions of decimal representation to Float representation:\\
\begin{tabular}{lll}
(1/10) asFloat & 0.1 & inexact (rounded to upper) \\
(1/5) asFloat & 0.2 & inexact (rounded to upper) \\
(0.1 + 0.2) asFloat & $\cdots$ & inexact (rounded to upper)  \\
\end{tabular}

3 inexact operations occurred, and, bad luck, the 3 rounding operations were all to upper, thus they did cumulate rather than annihilate.
On the other side, interpreting \ct{0.3} is causing a single rounding error (3/10) asFloat.
We now understand why we cannot expect \ct{0.1 + 0.2 = 0.3}.

As an exercise, you could show why \ct{1.3} *  \ct{1.3} $\neq$ \ct{1.69}.


\section{With floats, printing is inexact}

One of the biggest trap we learned with above example is that despite the fact that \ct{0.1} is printed \ct{'0.1'} as if it were exact, it's not.
The name \ct{absPrintExactlyOn:base:} used internally by \ct{printString} is a bit confusing, it does not print exactly,
but it prints the shortest decimal representation that will be rounded
to the same Float when read back (Pharo always converts the decimal representation to the nearest Float).

Another message exists to print exactly, you need to use \ct{printShowingDecimalPlaces:} instead.
As every finite Float is  represented internally as a Fraction with a
denominator being a power of 2, every finite Float has a decimal
representation with a finite number of decimals digits (just multiply
numerator and denominator with adequate power of 5, and you'll get the
digits). Here you go:

\begin{code}{}
0.1 asTrueFraction denominator highBit.
	-->  56
\end{code}

This means that the fraction denominator is $2^{55}$ and that you need 55 decimal digits after the decimal point to really print internal representation of \ct{0.1} exactly.
\begin{code}{}
0.1 printShowingDecimalPlaces: 55.
	--> '0.1000000000000000055511151231257827021181583404541015625'
\end{code}
And you can retrieve the digits with:
\begin{code}{}
0.1 asTrueFraction numerator * (5 raisedTo: 55).
	-->  1000000000000000055511151231257827021181583404541015625
\end{code}
You can just check our result with:
\begin{code}{}
1000000000000000055511151231257827021181583404541015625/(10 raisedTo: 55) =  0.1 asTrueFraction
	--> true
\end{code}
You see that printing the exact representation of what is implemented in machine would be possible but would be cumbersome. Try printing \ct{1.0e-100} exactly if not convinced.



\section{Float rounding is also inexact}
While float equality is known to be evil, you have to pay attention to other aspects of floats. Let us illustrate that point with the following example.

\begin{code}{Float truncation can be surprising}
2.8 truncateTo: 0.01
	--> 2.8000000000000003

2.8 roundTo: 0.01
	--> 2.8000000000000003
\end{code}

It is surprising but not false that \ct{2.8 truncateTo: 0.01} does not return \ct{2.8} but \ct{2.8000000000000003}. This is because \ct{truncateTo:} and \ct{roundTo:} perform several operations on floats: inexact operations on inexact numbers can lead to cumulative rounding errors as you saw above, and that's just what happens again.

Even if you perform the operations exactly and then round to nearest Float, the result is inexact because of the initial inexact representation of \ct{2.8}  and  \ct{0.01}. 
\begin{code}{}
(2.8 asTrueFraction roundTo: 0.01 asTrueFraction) asFloat
	--> 2.8000000000000003
\end{code}

Using \ct{0.01s2} rather than  \ct{0.01} let this example appear to work:
\begin{code}{}
2.80 truncateTo: 0.01s2
	-->  2.80s2
	
2.80 roundTo: 0.01s2
	-->  2.80s2
\end{code}

But it's just a case of luck, the fact that \ct{2.8} is inexact is enough to cause other surprises as illustrated below:
\begin{code}{}
2.8 truncateTo: 0.001s3.
	--> 2.799s3
	
2.8 < 2.800s3.
	--> true
\end{code}

Truncating in the Float world is absolutely unsafe. Though, using a ScaledDecimal for rounding is unlikely to cause such discrepancy, except when playing with last digits.



\section{Fun with inexact representations}
To add a nail to the coffin, let's play a bit more with inexact representations. Let us try to see the difference between different numbers: 

\begin{code}{}
{
((2.8 asTrueFraction roundTo: 0.01 asTrueFraction) - (2.8 predecessor)) abs -> -1.
((2.8 asTrueFraction roundTo: 0.01 asTrueFraction) - (2.8)) abs -> 0.
((2.8 asTrueFraction roundTo: 0.01 asTrueFraction) - (2.8 successor)) abs -> 1.
} detectMin: [:e | e key ]

--> 0.0->1
\end{code}

The following expression returns \ct{0.0->1}, which means that:
 \ct{(2.8 asTrueFraction roundTo: 0.01 asTrueFraction) asFloat = (2.8 successor)}

But remember that 

\begin{code}{}
(2.8 asTrueFraction roundTo: 0.01 asTrueFraction) ~= (2.8 successor)
\end{code}

It must be interpreted as the nearest Float to \ct{(2.8 asTrueFraction roundTo: 0.01 asTrueFraction)} is \ct{(2.8 successor)}.

If you want to know how far it is, then get an idea with:

\begin{code}{}
((2.8 asTrueFraction roundTo: 0.01 asTrueFraction) - (2.8 successor asTrueFraction)) asFloat
	--> -2.0816681711721685e-16
\end{code}




%%\section{About Scaled Decimal}
%%
%Why 
%(0.1 asScaledDecimal: 2) + (0.2 asScaledDecimal: 2) = (0.3 asScaledDecimal: 2)
%returns false 
%while
%0.1s2 + 0.2s2 = 0.3s2
%returns true
%There is a difference between these two:
%
%(1/10) asScaledDecimal: 2.
%(1/10) asFloat asScaledDecimal: 2.

%and 0.1s2 because the NumberParser knows you want a ScaledDecimal with the
%s2 specification, so it just doesn't use an intermediate Float, but
%directly convert (1/10) asScaledDecimal: 2.
%You can also try ScaledDecimal readFrom: '0.1', it avoids a an
%intermediate conversion to Float too.


%ScaledDecimals. they are Fractions and you can can look at their internal representation by using asFraction. 
%Numbers such as 0.1s2 are fractions built from scratch, hence \ct{0.1s2 asFraction} returns \ct{1/10} while (\ct{0.1 asScaledDecimal: 2) asFraction} returns the same thing as \ct{0.1 asFraction}.
%
%\ct{aFloat asFraction} is constructed in such a way that \ct{aFloat asFraction asFloat = aFloat}.
%for every aFloat. 
%
%At least with a probability of (100 - epsilon)\% with epsilon going to 0. while epsilon is not equal to 0 according to the author of asFraction (see the last comment at the end of Float>>asTrueFraction), obviously nobody has as yet found a counterexample, hence i would forget that.
%
%\ct{(0.1 asScaledDecimal: 2)} represents exactly the same number as the float \ct{0.1}.
%
%since \ct{0.1 + 0.2 = 0.3} returns false  you would of course expect your first example to return false and you would expect the error in the equality to be similar to the error in 0.1 + 0.2 = 0.3. although not necessarily equal. 
%If this last statement does not immediately make sense, you might want to think a second or two about the results of these things:
%\begin{code}{}
%((0.1 asFraction ) + (0.2 asFraction)) asFloat  = (0.1 +0.2) .
%((0.1 asFraction ) + (0.2 asFraction))   = (0.1 +0.2) asFraction .
%\end{code}
%
%
%%1) closeTo: good idea, i always found the threshold arbitrary.
%%
%%2) (0.1 asScaledDecimal:2 ) is exactly (0.1 asTrueFraction asScaledDecimal: 2)
%%So just evaluate (0.1 asFraction) + (0.2 asFraction) = (0.3 asFraction).
%%0.1 is a Float and is inexact because it result from this inexact
%%operation: 1/10.0.
%%
%%Even if by chance we would have  (0.1 asFraction) + (0.2 asFraction) =
%%(0.3 asFraction), then it would still be something different from
%%0.3s2, 0.3 = (3/10) -> false, 0.3s2 = (3/10) -> true.
%%
%%3) significand: yes, maybe we can write that (f significand
%%timesTwoPower: f exponent) = f is true for every finite Float (can't
%%remember if significand includes the sign, otherwise it would be f
%%sign * f significand timesTwoPower: f exponent)
%%
%%4) 1/10 and 1/5 : they will share the same significand (mantissa)
%%because (1/5) / 2.0 = (1/10.0), and = (1/5.0 timesTwoPower: -1).
%%Multiplying or dividing by a power of two just changes the exponent
%%and is an exact operation (unfortunately, 1/5 isn't).
%%
%%Hope that helps...
%%
%%cheers
%%
%%There is a difference between these two:
%%
%%(1/10) asScaledDecimal: 2.
%%(1/10) asFloat asScaledDecimal: 2.
%%
%%asFloat introduces a rounding error.
%%Though the result prints 0.1, it is different from (1/10), asFrank
%%said, just convert it back asTrueFraction, and you'll see.
%%asScaledDecimal: does not undo the rounding error, it just uses asTrueFraction.
%%If you want to undo the error, you may try (0.1 roundTo: 0.01s2).
%%
%%Nicolas
%%
%%
%%
%%Ah, because the NumberParser knows you want a ScaledDecimal with the
%%s2 specification, so it just doesn't use an intermediate Float, but
%%directly convert (1/10) asScaledDecimal: 2.
%%You can also try ScaledDecimal readFrom: '0.1', it avoids a an
%%intermediate conversion to Float too.
%%
%%
%%Oh, but they are:
%%
%%(1 to: 1000) detect: [:i |
%%	| s1 s2 |
%%	s1 := Number readFrom: '0.' , (String new: i withAll: $0) , '1'.
%%	s2 := Number readFrom: '0.' , (String new: i withAll: $0) , '2'.
%%	s1+s1~=s2]
%%->  308
%%
%%Thus 0.000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000001
%%+ 0.000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000001
%%~= 0.000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000000002
%%
%
%
%First, is FixedDecimal used in the Money package?  I didn't see is referenced there, so I'm curious where it came from.
%
%There is a definite different between ScaledDecimal and FixedDecimal.  ScaledDecimal  (unless it's been changed recently) keeps the full precision of the original number.  FixedDecimal (and Money, from my short perusal of it), do not - they make sure that the displayed value is exactly what the internal value is.  As an example, check out the following:
%
%x := ((1/3) asScaledDecimal: 2).
%x 		 		 				==> 0.33s2
%x = (1/3)							==> true
%((33/100) asScaledDecimal: 2)			==> 0.33s2
%x = ((33/100) asScaledDecimal: 2)		==> false (!)
%x + x								==> 0.67s2 (!)
%
%In general, the last 2 statements are NOT what you want when dealing with Money - if you have an amount, it is a definite amount, and not an approximate amount.  (With the possible exceptions of interim results - but for those, you probably need strict control over exactly how precise the interim results are - which isn't readily available in ScaleDecimal).  This is the main reason I built the FixedDecimal package on SqueakSource.  There, the above statements show:
%
%x := ((1/3) asFixedDecimal: 2).
%x 		 		 				==> 0.33
%x = (1/3)							==> true
%((33/100) asFixedDecimal: 2)			==> 0.33
%x = ((33/100) asFixedDecimal: 2)			==> true
%x + x								==> 0.66
%
%From my perspecive, ScaledDecimal is great when you want to keep as much precision as possible, but need to SHOW less precision.  It reminds me of science, where you have this measurement that is way more precise than you can really measure, so you 'scale' it back to a precision that is reliable.
%
%
%\section{Chapter Summary}
%Floats are inexact numbers. Pay attention when you manipulate them.
%We have reviewed the following points:
%
%\begin{itemize}
%\item Never compare float equality. For the comparison, use \ct{closeTo:} instead.
%\item The method  \ct{closeTo:} has a precision of 0.0001.
%\item A Float is composed by 1 bit of sign, 11 bits of exponent and 52bits of mantissa (which is a fractional number expressed in base two).
%\item A Float is represented in base 2 by: \[ sign \cdot mantissa \cdot 2^{exponent} \]
%\item The method \ct{significand} is used to know the mantissa.
%\end{itemize}


\section{Chapter summary}

Floats are approximation of a real number by being able to support a wide range of decimal values. This chapters has reviewed the following points

\begin{itemize}
\item Never use \ct{=} to compare floats (\eg \ct{(0.1 + 0.2) = 0.3} returns \ct{false})
\item Use \ct{closeTo:} instead (\eg \ct{(0.1 + 0.2) closeTo: 0.3} returns \ct{true})
\item A float number is represented in base as \emph{sign} \ct{x} \emph{mantissa} \ct{x} $2^{exponent}$ (\eg \emph{1.2345 = 12345} \ct{x} \emph{10}$^{-4}$)
\item \ct{truncateTo:} and \ct{roundTo:} do not always work when truncating or rounding up float (\eg \ct{2.8 roundTo: 0.01} returns \ct{2.800...003})
\end{itemize}


There are much more things to know about floats, and if you are advanced enough, it would be a good idea to check this link from the wikipedia page \href{http://www.validlab.com/goldberg/paper.pdf}{"What Every Computer Scientist Should Know About Floating-Point Arithmetic" (http://www.validlab.com/goldberg/paper.pdf)}.


%%=========================================================
\ifx\wholebook\relax\else
   \bibliographystyle{jurabib}
   \nobibliography{scg}
   \end{document}
\fi

%=================================================================
\ifx\wholebook\relax\else\end{document}\fi
%=================================================================

%-----------------------------------------------------------------

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: t
%%% TeX-PDF-mode: t
%%% ispell-local-dictionary: "english"
%%% End: